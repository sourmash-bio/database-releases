DOMAINS=['archaea',
         'fungi',
         'protozoa',
         'bacteria',
         'viral']

TAG='latest'
DATABASES = ['/group/ctbrowngrp/sourmash-db/wort-manifests/2023-05-31.wort.sqlmf']
KSIZES=[21,31,51]
LOGS='logs'

rule all:
    input:
        expand("{D}.missing.csv", D=DOMAINS),
        expand("{D}.manifest.csv", D=DOMAINS),
        expand("{domain}.lineages.csv", domain=DOMAINS),

rule build:
    input:
        expand("genbank-latest-{D}-k{k}.zip", D=DOMAINS, k=KSIZES)

rule check:
    input:
        expand("genbank-latest-{D}-k{k}.zip.check", D=DOMAINS, k=KSIZES)

rule tax:
    input:
        expand("{domain}.lineages.csv", domain=DOMAINS)

rule download_assembly_summary:
    output:
        '{D}.assembly_summary.txt'
    shell: """
        curl -L https://ftp.ncbi.nlm.nih.gov/genomes/genbank/{wildcards.D}/assembly_summary.txt > {output}
    """

rule make_idents:
    input:
        '{D}.assembly_summary.txt'
    output:
        "{D}.idents.csv"
    shell: """
        echo ident > {output}
        cut -f1 {input} | grep -v ^# >> {output}
    """

rule picklist_check:
    input:
        databases = DATABASES,
        picklist = "{D}.idents.csv",
    output:
        missing = "{D}.missing.csv",
        manifest = "{D}.manifest.csv",
    log: f"{LOGS}/{{D}}.picklist-check.log"
    benchmark: f"{LOGS}/{{D}}.picklist-check.benchmark"
    shell: """
        sourmash sig check --picklist {input.picklist}:ident:ident \
            {input.databases} --output-missing {output.missing} \
            --save-manifest {output.manifest} 2> {log}
        touch {output.missing}
    """

rule picklist_confirm:
    input:
        picklist = "{D}.idents.csv",
        zip = "genbank-latest-{D}-k{k}.zip",
    output:
        confirm = touch("genbank-latest-{D}-k{k}.zip.check")
    log: f"{LOGS}/{{D}}-k{{k}}.picklist-confirm.log"
    benchmark: f"{LOGS}/{{D}}-k{{k}}.picklist-confirm.benchmark"
    shell: """
        sourmash sig check --picklist {input.picklist}:ident:ident \
            {input.zip} --fail 2> {log}
    """

rule build_zip:
    input:
        databases = DATABASES,
        manifest = "{D}.manifest.csv",
    output:
        "genbank-latest-{D}-k{k}.zip"
    log: f"{LOGS}/{{D}}-{{k}}.build-zip.log"
    benchmark: f"{LOGS}/{{D}}-{{k}}.build-zip.benchmark"
    shell: """
        sourmash sig cat {input.manifest} -k {wildcards.k} -o {output} 2> {log}
    """

# taxonomy rules, from https://github.com/ctb/2022-assembly-summary-to-lineages
rule download_ncbi_utils:
    output: "ncbi_taxdump_utils.py"
    shell:
        "curl -JLO https://raw.githubusercontent.com/ctb/2022-assembly-summary-to-lineages/main/ncbi_taxdump_utils.py"

rule download_taxscript:
    output: "make-lineage-csv.py"
    shell:
        "curl -JLO https://raw.githubusercontent.com/bluegenes/2022-assembly-summary-to-lineages/virus-tax/make-lineage-csv.py"

rule download_taxdump: # may need to restart this a couple times
    output:
        "taxdump/nodes.dmp",
        "taxdump/names.dmp"
    shell:
        "curl -L ftp://ftp.ncbi.nlm.nih.gov/pub/taxonomy/taxdump.tar.gz | (mkdir -p taxdump && cd taxdump && tar xzvf -)"

rule make_lineage_csv:
    input:
        "{name}.assembly_summary.txt",
        "taxdump/nodes.dmp",
        "taxdump/names.dmp",
        "make-lineage-csv.py",
        "ncbi_taxdump_utils.py",
    output:
        "{name}.lineages.csv"
    params:
        ictv_cmd = lambda w: " --ictv " if 'viral' in w.name else '',
    shell:
        "python make-lineage-csv.py taxdump/{{nodes.dmp,names.dmp}} {input[0]} -o {output} {params.ictv_cmd}"

